/* Include s3d symbols */
#include <Script3d.h>

#include "../core/Player/PlayerModel.s3d"

/**
 * SERVER CONFIGURATION
 */
#define CONF_SERVER_ADDRESS "localhost"	/**< address of the server */
#define CONF_SERVER_PORT 5555			/**< port of the server. */
#define CONF_MAX_NPLAYERS 2				/**< max number of players. */

/**
 * Gloval variables
 */

var mylevel;	/**< Level Obj, main level */	

var listenCh;	/**< int, listen channel where clients first connects */

var clients = Array(CONF_MAX_NPLAYERS);	/**< Array of Array = {ch, player} all clients array
 										 * ch	int, channel for communication purpose
 										 * player	PlayerModel Obj, player model of the client
 										 */
var freeIDs = Array(0);	/**< int Array, free id to assign to new players */

/**
 * delete the first free id from freeIDS
 * 
 * @return int, first free id
 */
function delFirstFreeID() {
	if (len(freeIDs)>0) {
		var id = freeIDs[0];
		adel(freeIDs, 0);
		return id;
	}
}

/**
 * add an id to freeIDS
 * 
 * @param[in]	ID	int, Id to add
 */
function addFreeID(ID) {
	aadd(freeIDs, ID);
}

/** ##############################################################
 *		FUNCTIONS TO COMMUNICATE WITH CLIENTS 
 * ############################################################### */

/* accept a new connection with a client
 * 
 * @return client channel, eventual error
 */ 
function acceptNewClient();

/**
 * Receive data from client channel
 * 
 * @return data received
 */
function receiveDataFromClient(clientCh);

/**
 * send data to a client
 * 
 * @param[in]	clientCh	int, channel to use for send
 * @param[in]	data		generic var, data to send
 */
function sendDataToClient(clientCh, data);

/**
 * send data to all clients
 * 
 * @param[in]	data		generic var, data to send
 */
function sendDataToAllClients(data);

/**
 * send models of all players(player.Marshall) to all client
 * 
 */
function sendAllModelsToAllClients();

/**
 * initialize a new client
 */
function initNewClient();

/**
 * close the connection with a disconnected client
 */
function clientClose(client);

/**
 * check collision between shooted bullets of a player and all other entities
 * 
 * Remove the bullets that collides(bullet = void)
 * 
 * @param[in]	pp	player whom to check bullets
 */
function processBulletsCollision(pp);

/**
 * return a x,y position of the level where there isn't a wall
 * 
 * @return	vector, xy free of wall position 
 */
function getRandomFreePosition();

//
function OnDownload() {
	FileDownload("../XVRResources.zip");
}

//
function OnInit(params) {
	OutputLN("");
	OutputLN("init server");

	OutputLN("init server level");
	mylevel = level();
	mylevel.init("level.txt");
	OutputLN("server level initialized");
	
	OutputLN("creating listen channel");
	listenCh = NetCreateChannelTCP(CONF_SERVER_PORT, CONF_SERVER_ADDRESS);
	if (listenCh == -1) {
		OutputLN("error NetCreateChannelTCP");
		Quit(listenCh);
	}
	OutputLN("listen channel created");
	
	for(var i=0; i<CONF_MAX_NPLAYERS; i++) {
		aadd(freeIDs, i);
	}
	
	SetTimeStep(200);

}

//
function OnFrame() {
	var isDataReady;
	var update = false;
	
	// check new player connection
	isDataReady = NetDataReady(listenCh);
	if(isDataReady) {
		OutputLN("new client connected");
		var ret = initNewClient();
		var pid = ret[0], error = ret[1];
		if (error != void) {
			Quit(error);
		}
		sendAllModelsToAllClients();
	}
	// update players models
	for (var i = 0; i < len(clients); i++) {
		if (clients[i] == void) {
			continue;
		}
		
		var ch = clients[i][0], player = clients[i][1];
	
		var data = 0;	
		isDataReady = NetDataReady(ch);			
		if (isDataReady) {
			data = receiveDataFromClient(ch);
			if (data == void){
				// client disconnected
				clientClose(i);
				continue;
			}
		}
		
		if (valtype(data) == "S") {
			if (data == "restart" && player.IsDead()) {
				player.Restart(getRandomFreePosition());
			}
			data = 0;
		}
		
		processBulletsCollision(player);
		player.Update(data);
	}
		
}

//
function OnTimer() {
	static var oldplayerModels = Array(CONF_MAX_NPLAYERS);
	static var c = 0;
//	OutputLN(c);
	c++;
	if (c%5 != 0) {
		return;
	}
	
	var playerDeltaModels = Array(CONF_MAX_NPLAYERS);
	var update = false;
	for (var i = 0; i < len(clients); i++) {
		if (clients[i] == void) {
			if (oldplayerModels[i] != void) {
				if (len(oldplayerModels[i]) != 0) {
					oldPlayerModels[i] = {};
					update = true;	
				} else {
					oldplayerModels[i] = void;
				}	
			}
			continue;
		}
		
		var ch = clients[i][0], player = clients[i][1];
		
		if (oldplayerModels[i] == void) {
			playerDeltaModels[i] = player.Marshall();
		} else {
			playerDeltaModels[i] = player.MarshallDelta(oldPlayerModels[i]); 
		}
		
		if (playerDeltaModels[i] != void) {
			update = true;
		}
		
		oldPlayerModels[i] = player.Marshall();
		player.RemoveVoidBullets();
	}
	
	if(update) {
		sendDataToAllClients(playerDeltaModels);	
	}
}

//
function processBulletsCollision(pp){
	var bullets = pp.shootedBullets;
	 
	for (var i=0; i<len(bullets); i++) {
		var bullet = bullets[i];
		
		if (bullet == void) {
			continue;
		}
		
		for (var j=0; j<len(clients); j++) {
			if (clients[j] == void) {
				continue;
			}
			var ch = clients[j][0], player = clients[j][1];
			
			if (bullet.pid == player.ID) {
				continue;
			}
				
			if (player.isCollidingWithBullet(bullet)) {
				player.Hp -= bullet.damage;
				outputLn("player ", pp.ID, " hit player ", player.ID);
				if (player.Hp <= 0){
					// player dead; do something ??
					OutputLn("player ", pp.ID, " kill player ", player.ID);
				}
				pp.shootedBullets[i] = void;
				break;
			}
		}
	}
	// collision with walls
	for (var i=0; i<len(bullets); i++) {
		var bullet = bullets[i];

		if(bullet == void) {
			continue;
		}
		
		var wall = mylevel.GetWallObjAtPosition(bullet.position);
		if (wall != void) {
			pp.shootedBullets[i] = void;
		}
	}
}

//
function getRandomFreePosition() {
	var maxVal = len(myLevel.freeIndexes);
	var freeIndex = myLevel.freeIndexes[Rand(maxVal)];
	var pos = [freeIndex[1], 0, freeIndex[0]];
	
	return pos;
}

//
function acceptNewClient(){
	var clientCh = -1;
	while(clientCh == -1){
		clientCh = NetAcceptTCP(listenCh, VR_NO_BLOCKING);	
		if (clientCh == 0){
			return {clientCh, "NetAcceptTCP: " + str(clientCh)};
		}
	}
	return {clientCh, void};
}

//
function receiveDataFromClient(listenCh) {
	var data = NetVarReceiveFromTCP(listenCh);
	OutputLN("receiveDataFromClient: ", data);
	return data;
}

//
function sendDataToClient(outputCh, data) {
	NetVarSendToTCP(outputCh, data);
}

//
function sendDataToAllClients(data) {
	foreach(var client in clients) {
		if (client != void) {
			var ch = client[0];
			sendDataToClient(ch, data);	
		}
	}
}

//
function sendAllModelsToAllClients() {
	var playerModels = Array(CONF_MAX_NPLAYERS);
	for (var i = 0; i < len(clients); i++) {
		if (clients[i] == void) {
			playerModels[i] = void;
			continue;
		}

		var player = clients[i][1];
		playerModels[i] = clients[i][1].Marshall();
    }
    sendDataToAllClients(playerModels);
}

//
function initNewClient() {
	var ret = acceptNewClient();
	var clientCh = ret[0], err = ret[1];
	if (err != void) {
		OutputLN("acceptNewClient: ", err);
		return {void, Sprintf("acceptNewClient: %s\n", err)};
	}
	// initialize a player model
	var pid = delFirstFreeID();
	var p = PlayerModel(mylevel, pid, getRandomFreePosition());
	clients[pid] = {clientCh, p};
	// responce to client
	var model = p.Marshall();
	sendDataToClient(clientCh, model);
	return {pid, void};
}

//
function clientClose(id) {
	var client = clients[id];
	clients[id] = void;
	addFreeID(id);
	var ch = client[0], p = client[1];
	NetClose(ch);
//	TODO: p.free???
}

//
function DownloadReady(RequestID){}

//
function OnEvent(eventID, wparam, lparam){}

//
function OnError(){}

//
function OnExit() {}
